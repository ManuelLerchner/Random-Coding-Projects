//{{V2}}
ORGANIZATION_BLOCK OB1
TITLE= "Zyklisches Hauptprogramm"


AUTHOR:nb
FAMILY:nb
NAME:nb
VERSION:1.0


VAR_TEMP
       OB1_EV_CLASS:BYTE //Bits 0-3 = 1 (Coming event), Bits 4-7 = 1 (Event class 1)
       OB1_SCAN_1:BYTE //1 (Cold restart scan 1 of OB 1), 3 (Scan 2-n of OB 1)
       OB1_PRIORITY:BYTE //1 (Priority of 1 is lowest)
       OB1_OB_NUMBR:BYTE //1 (Organization block 1, OB1)
       OB1_RESERVED_1:BYTE //Reserved for system
       OB1_RESERVED_2:BYTE //Reserved for system
       OB1_PREV_CYCLE:INT //Cycle time of previous OB1 scan (milliseconds)
       OB1_MIN_CYCLE:INT //Minimum cycle time of OB1 (milliseconds)
       OB1_MAX_CYCLE:INT //Maximum cycle time of OB1 (milliseconds)
       OB1_DATE_TIME:DATE_AND_TIME //Date and time OB1 started
END_VAR
BEGIN

NETWORK
TITLE= 
//{{a}}

////////// 
//Eingabe/ 
////////// 
       U    E0.0
       FP   M      1000.0
       SPBN read

//Get Sekunden 
       L    EW       10
       L    2#0000000011111111
       UW
       BTI
       T    MW12

//Get Minuten 
       L    EW       10
       L    2#1111111100000000
       UW
       SRW            8
       BTI
       T    MW10
         
//Calculate Total Millis 
       L    MW10
       L    60
       *I
       L    MW12
       +I
       ITD
       L    1000
       *D
       T    MD100

//Change Millis to S5T 
       CALL FC       40
         IN:=MD100
         RET_VAL:=MW14

read:  NOP  0
NETWORK
TITLE= 
//{{a}}

/////////// 
//RestZeit/  
/////////// 
       U    A0.0
       SPBN rest

//Get RestTime 
       LC   T         0
       T    MW16

// Change RestTimeS5T to Millis 
       CALL FC       33
         IN:=MW16
         RET_VAL:=MD104

//Write RestTime to AW10; Format: MM:SS 
       CALL FC       50
         Zeit_MilliSekunden:=MD104
         Zeit_Minuten_Sekunden:=AW10


rest:  NOP  0
NETWORK
TITLE=
//{{a}}

/////////// 
//VergangeneZeit   
///////////  
       U    A0.0
       SPBN verg

//Calculate Difference 
       L    MD100
       L    MD104
       -D
       T    MD108

//Write VergangeneZeit to AW12; Format: MM:SS  
       CALL FC       50
         Zeit_MilliSekunden:=MD108
         Zeit_Minuten_Sekunden:=AW12
 
verg:  NOP  0
NETWORK
TITLE= 
//{{a}}

///////// 
//Timer//    
/////////   

//Start Timer 
       U    E0.0
       FP   M      1000.1
       R    A0.1
       L    MW14
       SV   T         0

//Stop Timer 
       U    E0.1
       R    T         0

//Timer läuft? 
       U    T         0
       =    A0.0

//Timer abgelaufen? 
       U    T         0
       FN   M      1000.2
       S    A0.1
END_ORGANIZATION_BLOCK
